<h1> 單晶片網頁實做筆記

<h3> 系級：資工二   學號：111010506     姓名：林維新

<h2> 摘要

>   此為單晶片實作筆記，將在Linklt 7697上實做簡單的網頁伺服器，以Aroudino轉寫程式與Protel 99se設計電路。
將以圖片的方式呈現實做結果與電路設計圖，另附上此次實作的程式碼。
>   在實作上需具備基礎的硬體連接能力、網頁設計能力和電路設計能力，需要使用C/C++、html、css等語言。
>   此文章在撰寫中會使用到ChatGTP，如有使用將用（＝＝）標註，表示此段文字由GTP撰寫。

<h2> 簡單網頁製作

> 製作一個簡單網頁練習讓單晶片與電腦連接。
<p>以下為程式碼，將在其中使用註解的方式講解
<pre><code>
    //引入溫濕度感應器的函式庫
    #include <DHT.h>
    //引入 LWiFi 函式庫，該函式庫用於處理與WiFi相關的功能。
    #include <LWiFi.h>  

    //宣告兩個浮點數陣列 temp 和 humid
    float temp[10] = {0};
    float humid[10] = {0};

    int data_count = 0;
    int data_max = 5;

    //(ssid 存儲Wi-Fi網絡的SSID，pass 存儲Wi-Fi網絡的密碼，status 變數存儲Wi-Fi連接的狀態。)
    char ssid[] = "319";
    char pass[] = "sbHappy319319";
    int status = WL_IDLE_STATUS;

    DHT dht(2, DHT11);

    //創建了一個 WiFiServer 物件，使用端口8080。
    WiFiServer server(8080); 

    void setup()
    {
        // put your setup code here, to run once:
        Serial.begin(9600);
        Serial.print("connected to linkit 7697");

        while (!Serial)
        {
            ;
        }

        while (status != WL_CONNECTED)
        {
            Serial.print("Attempting to connect to SSID: ");
            Serial.println(ssid);
            status = WiFi.begin(ssid, pass);
        }

        print_wifi_status();

        dht.begin();
        server.begin(); // Listening for client connections
        delay(3000);
    }

    void loop()
    {
        // put your main code here, to run repeatedly:
        delay(5000);

        //讀取溫度和濕度數值並存儲到 temperature 和 humidity 變數中。
        float temperature = dht.readTemperature();
        float humidity = dht.readHumidity();

        Serial.print("data count: ");
        Serial.println(data_count);
        Serial.print(" temperature: ");
        Serial.print(temperature);
        Serial.print(" humidity:");
        Serial.println(humidity);

        //將最新的溫度和濕度數值存儲到 temp 和 humid 陣列中的最後一個位置。
        temp[data_max] = temperature;
        humid[data_max] = humidity;

        for (int i = 0; i < data_max; i++)
        {
            temp[i] = temp[i + 1];
            humid[i] = humid[i + 1];
        }

        display(temp, humid);

        WiFiClient client = server.available(); // Accept client connection
        if (client)
        {
            Serial.println("a new client");
            boolean current_line_is_blank = true;
            String current_line = "";
            while (client.connected())
            {
                if (client.available())
                {
                    char c = client.read(); // Receive message
                    Serial.write(c);        // Serial.print(c);
                    delay(50);
                    if (c == '\n' && current_line_is_blank)
                    {                                // no remaining character to read (\r\n\r\n)

                        //向客戶端發送HTTP回應，包括響應頭部和HTML內容。
                        client.println(res_header()); // Send message
                        client.println();            // Send message
                        client.print(convert_html(temp, humid)); // Send message
                        break;
                    }
                    if (c == '\n')
                    {
                        current_line_is_blank = true;
                        current_line = "";
                    }
                    else if (c != '\r')
                    {
                        current_line_is_blank = false;
                        current_line += c;
                    }
                }
            }
            delay(1);

            client.stop(); // Close connection
            Serial.println("client disonnected");
        }

        if (data_count >= data_max)
        {
            data_count = 0;
        }
        else
        {
            data_count += 1;
        }
    }

    //(display 函式用於顯示 temp 和 humid 陣列中的數值。)
    void display(float temp[], float humid[])
    {
        for (byte i = 0; i < data_max; i++)
        {
            Serial.print("temp[");
            Serial.print(i);
            Serial.print("]");
            Serial.print(temp[i]);

            Serial.print("  humid[");
            Serial.print(i);
            Serial.print("]");
            Serial.println(humid[i]);
        }
        Serial.println("");
    }

    //(print_wifi_status 函式用於顯示Wi-Fi連接的狀態和IP地址。)
    void print_wifi_status()
    {
        Serial.print("SSID: ");
        Serial.println(WiFi.SSID());

        IPAddress ip = WiFi.localIP();
        Serial.print("IP Address: ");
        Serial.println(ip);

        long rssi = WiFi.RSSI();
        Serial.print("signal strength (RSSI):");
        Serial.print(rssi);
        Serial.println(" dBm");
    }

    //(res_header 函式返回HTTP回應的頭部內容。)
    String res_header()
    {
        String http_stats = "HTTP/1.1 200 OK\n";
        String cnt_type = "Content-Type: text/html\n";
        String cntion = "Connection: close\n";
        //  String refsh = "Refresh: 2";

        //  String res_header = http_stats + cnt_type + cntion + refsh;
        String res_header = http_stats + cnt_type + cntion;
        return res_header;
    }

    //(convert_html 函式返回HTML內容，包含溫度和濕度數值。)
    String convert_html(float temp[], float humid[])
    {

        // 以下為網頁的撰寫，可以看到程式碼是以String儲存了每一行網頁程式碼。
        String doctype = "<!DOCTYPE HTML>\n";
        String head1 = "<html>\n";
        String head2 = "<head>\n";
        String head3 = "<meta charset=\"utf-8\">\n";
        String head4 = "<title>7697Server</title>\n";
        String head5 = "</head>\n";

        String head = doctype + head1 + head2 + head3 + head4 + head5;

        String body1 = "<body>\n";
        String body2 = "<center><h1>Homework Website as server</h1></center>\n";
        String body3 = "<div>\n";
        String body4 = "<center><p>Group menber: WU CHEN ZHI, LIN WEI SHIN<br>\n";
        String body5 = "Advisor: CHEN CHENG TE<br>\n";
        String body11 = "";
        for (int i = 0; i < data_max; i++)
        {
            body11 = body11 + "number: " + i + ", temperature: " + temp[i] + ", humid:" + humid[i] + "<br>";
        }
        String body6 = "\nNational Quemoy University Department of Computer Science and Information Engineering\n ";
        String body7 = "</p></center>\n";
        String body8 = "</div>\n";
        String body9 = "</body>\n";
        String body10 = "</html>\n";

        String body = body1 + body2 + body3 + body4 + body5 + body11 + body6 + body7 + body8 + body9 + body10;
        String html = head + body;
        return html;
    }
</code></pre>

<h3> 最後將啟動我們的7697並將自己的電腦與7697設定在同一網路下。等待7697啟動並連至網路，就可以看到網頁。
